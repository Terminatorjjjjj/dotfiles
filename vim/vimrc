set nocompatible " must be first

" Runtime Path: {{{

set runtimepath+=~/.vim/bundle/supertab " ervandew/supertab
set runtimepath+=~/.vim/bundle/vim-easy-align " junegunn/vim-easy-align
set runtimepath+=~/.vim/bundle/rainbow_parentheses.vim " kien/rainbow_parentheses.vim
set runtimepath+=~/.vim/bundle/VisIncr " vim-scripts/VisIncr
set runtimepath+=~/.vim/bundle/ultisnips " SirVer/ultisnips
set runtimepath+=~/.vim/bundle/vim-unimpaired " tpope/vim-unimpaired
set runtimepath+=~/.vim/bundle/vim-fugitive " tpope/vim-fugitive
set runtimepath+=~/.vim/bundle/ctrlp.vim " ctrlpvim/ctrlp.vim

" }}}
" Basic Setting: {{{

syntax enable
syntax on
filetype on
filetype plugin indent on

set autoindent smartindent
set backspace=2
set expandtab softtabstop=-1 " use shiftwidth
set tabstop=8
set shiftwidth=4
set ruler
set showcmd
set nowrap
set incsearch hlsearch

" }}}
" More Setting: {{{

" do not redraw screen in the middle of the macro
set lazyredraw
" disable linebreak word wrap in insert mode
set formatoptions-=t
" persistent undo
set undofile
set history=50
set autochdir
set splitbelow splitright

set cursorline "cursorcolumn
augroup cursor_toggle
    autocmd!
    autocmd WinEnter * setlocal cursorline "cursorcolumn
    autocmd WinLeave * setlocal nocursorline "nocursorcolumn
augroup END

augroup cursor_insert
    autocmd!
    autocmd InsertEnter * setlocal cursorcolumn
    autocmd InsertLeave * setlocal nocursorcolumn
augroup END

set number relativenumber
augroup number_toggle
    autocmd!
    autocmd BufEnter,FocusGained,InsertLeave * set relativenumber
    autocmd BufLeave,FocusLost,InsertEnter * set norelativenumber
augroup END

augroup autocomplete_ignore_case
    autocmd!
    autocmd InsertEnter * set ignorecase
    autocmd InsertLeave * set noignorecase
augroup END

set backupdir=.vi_backup/,~/.vi_backup/,/tmp//
set directory=.vi_swp/,~/.vi_swp/,/tmp//
set undodir=.vi_undo/,~/.vi_undo/,/tmp//

" vertically center document when entering insert mode
augroup vertical_center_insert
    autocmd!
    autocmd InsertEnter * normal zz
augroup END

" make shell not source startup scripts when running shell command in vim
set shellcmdflag=-f\ -c

" }}}
" Wild Menu: {{{

" enable auto completion menu w/ tab & behave like bash
set wildmenu
set wildmode=list:longest,full
set wildignore=*.docx,*.jpg,*.png,*.gif,*.pdf,*.pyc,*.exe,*.flv,*.img,*.xlsx,*.zip,*.so,*.swp,*/tmp/*,*/.git/*

" }}}
" File Type: {{{

augroup set_file_type
    autocmd!
    autocmd BufNewFile,BufRead .bashrc*,bashrc,.bash_profile*,bash_profile* setfiletype sh
    autocmd BufNewFile,BufRead *.v,*.vh,*.vs setfiletype systemverilog
    autocmd BufNewFile,BufRead *.sv,*.svh    setfiletype systemverilog
    autocmd BufNewFile,BufRead *.v,*.vh,*.vs set syntax=systemverilog
    autocmd BufNewFile,BufRead *.sv,*.svh    set syntax=systemverilog
augroup END

" filename suffix for 'gf' keymap to search in path
augroup set_suffixesadd
    autocmd!
    autocmd Filetype systemverilog setlocal suffixesadd+=.v,.sv,.h,.svh
augroup END

" }}}

" Colorscheme: {{{

set background=dark
colorscheme gruvbox

" }}}
" Status Line: {{{

set laststatus=2

" modified from this blog:
"   https://jip.dev/posts/a-simpler-vim-statusline/
"   https://github.com/blaenk/dots/blob/dfb34f1ad78f5aa25bc486d3c14c9a0ef24094bd/vim/.vimrc#L168

function! Status(winnum)
    let active = a:winnum == winnr()
    let bufnum = winbufnr(a:winnum)

    function! Color(active, num, content, fill)
        if a:active
            " output the content with supplied colorgroup num, e.g. num=1 -> User1
            return '%' . a:num. '*' . a:content . '%*'
        else
            " tried using different num for flexibility but not working
            return a:fill . a:content . '%*'
        endif
    endfunction

    function! GitBranch()
        let br = trim(system("git -C " . expand("%:h") . " branch --show-current 2>/dev/null")) 
        if strlen(br) > 0
            return ' ' . br
        else
            return ''
    endfunction

    let fill = '%#Comment#'
    let buftype = getbufvar(bufnum, '&buftype')
    let bufname = bufname(bufnum)

    " handlftype alternative statusline
    if buftype ==# 'help'
        let helpname = ' ' . fnamemodify(bufname, ':t:r')
        let altstat = fill . ' »' . '%*'
        let altstat .= Color(active, 5, helpname, fill)
        let altstat .= fill . ' «'
        return altstat
    endif

    let branch = (active) ? GitBranch() : ''
    let separator = (strlen(branch)>0) ? ' »' : ''
    let filename = ' %<%f'
    let readonly = getbufvar(bufnum, '&readonly') ? ' ‼' : ''
    let modified = getbufvar(bufnum, '&modified') ? ' +' : ''
    let column = '%c '

    let stat = ''
    let stat .= Color(active, 1, branch, fill)
    let stat .= fill . separator . '%*'
    let stat .= Color(active, 2, filename, fill)
    let stat .= fill . ' »'
    let stat .= Color(active, 3, readonly, fill)
    let stat .= Color(active, 3, modified, fill)
    let stat .= fill
    let stat .= '%='
"     let stat .= fill . '« '
"     let stat .= fill . &filetype . ' '
    let stat .= fill . '« '
    let stat .= Color(active, 4, column, fill)
    let stat .= fill . ' '

    return stat
endfunction

function! s:RefreshStatus()
  for nr in range(1, winnr('$'))
    call setwinvar(nr, '&statusline', '%!Status(' . nr . ')')
  endfor
endfunction

augroup status
  autocmd!
  autocmd VimEnter,WinEnter,BufWinEnter * call <SID>RefreshStatus()
augroup END

hi link User1 Identifier
hi link User2 String
hi link User3 Number
hi link User4 Identifier
hi link User5 Normal
" }}}
" Key Binding: {{{

let mapleader='\'

inoremap jj <esc>
nnoremap <space> :
vnoremap <space> :

nnoremap Y y$
nnoremap gw <c-w>

" move line up/down (add ==, ==gi, gv=gv if want auto align indent)
nnoremap <c-j> :m .+1<cr>
nnoremap <c-k> :m .-2<cr>
inoremap <c-j> <esc>:m .+1<cr>gi
inoremap <c-k> <esc>:m .-2<cr>gi
vnoremap <c-j> :m '>+1<cr>gv
vnoremap <c-k> :m '<-2<cr>gv

" add semicolon to the end of current line w/o moving cursor
nnoremap <leader>; m'a;<esc>`'

" close terminal mode (only supported in vim8)
if v:version > 800
    tnoremap <esc> <c-\><c-n>:q!<cr>
endif

" keeping search result centered
nnoremap N Nzzzv
nnoremap n nzzzv

" horizontal movement w/ easier pressing
nnoremap H ^
nnoremap L $

" go to prev match history in command line
cnoremap <c-j> <down>
cnoremap <c-k> <up>

" replace the word under cursor
nnoremap <leader>* :%s/\<<c-r><c-w>\>//g<left><left>

" nnoremap gp :find<space>*

" remap since ; is used for fold toggle
nnoremap <space>; ;

" }}}
" Code Comment: {{{

augroup get_comment_leader
    autocmd!
    autocmd FileType c,cpp,java,scala,verilog,systemverilog,fotran,stata let b:comment_leader = '// '
    autocmd FileType sh,ruby,python,make,tmux,tcsh,csh,zsh,conf          let b:comment_leader = '# '
    autocmd FileType tex,matlab                                          let b:comment_leader = '% '
    autocmd FileType vim                                                 let b:comment_leader = '" '
augroup END

" comment and uncomment
noremap <silent> gcc :<c-b>silent <c-e>s/^/<c-r>=escape(b:comment_leader,'\/')<cr>/<cr>:nohlsearch<cr>
noremap <silent> gcu :<c-b>silent <c-e>s/^\v<c-r>=escape(b:comment_leader,'\/')<cr>//e<cr>:nohlsearch<cr>

" single keymap toggle
function! CommentToggle() range
    if getline(a:firstline) =~ '^\V' . escape(b:comment_leader, '\')
        silent execute a:firstline . ',' . a:lastline . 's/^\V' . escape(b:comment_leader, '\/') . '//e'
    else
        silent execute a:firstline . ',' . a:lastline . 's/^/' . escape(b:comment_leader, '\/') . '/e'
    endif
endfunction
command! -range CT <line1>,<line2>call CommentToggle()|nohlsearch

nnoremap <c-\> :CT<cr>
vnoremap <c-\> :CT<cr>
nnoremap gci :CT<cr>
vnoremap gci :CT<cr>

" }}}
" Fold: {{{

function! MyFoldText()
    let line = getline(v:foldstart)
    let sub1 = substitute(line, '/\*\|\*/\| {{{\d\=', '', 'g') " }}} for correct folding
    let sub2 = substitute(sub1, '^ \+', '', 'g')
    let folded_line_count = v:foldend - v:foldstart + 1
    return printf('%5d ...  %s  ...', folded_line_count, sub2)
endfunction

set foldmethod=marker
set foldmarker={{{,}}}
set foldlevel=0
set foldcolumn=0
set fillchars=fold:\  " a space after \
set foldtext=MyFoldText()

nnoremap ; za
vnoremap ; za

" }}}
" Splits: {{{

" move to next split
map <c-l> :wincmd w<cr>
" remove pipes | that act as separators on splits (there is a space after \)
set fillchars+=vert:\ 

" }}}
" Buffers: {{{

" to change buffer w/o saving current buffer
set hidden
" display buffer list and go to buffer
nnoremap gb :ls<cr>:b<space>
" display buffet list and open buffer in right split
nnoremap gs :ls<cr>:vert sb<space>
" go to last buffer
nnoremap <c-h> :b#<cr>

" }}}

" Plugin: Rainbow Parentheses {{{

augroup rainbow_parenthesis
    autocmd!
    autocmd VimEnter * RainbowParenthesesToggle
    autocmd Syntax * RainbowParenthesesLoadRound
    autocmd Syntax * RainbowParenthesesLoadSquare
    autocmd Syntax * RainbowParenthesesLoadBraces
augroup END

" }}}
" Plugin: Supertab {{{

let g:SuperTabMappingForward = '<s-tab>'
let g:SuperTabMappingBackward = '<tab>'

" }}}
" Plugin: Ultisnips {{{

let g:UltiSnipsSnippetsDir = "~/.vim/my_snippet"
let g:UltiSnipsSnippetDirectories = ["ulti_snippet", "my_snippet"]
let g:UltiSnipsExpandTrigger = "<tab>"
let g:UltiSnipsJumpForwardTrigger = "<tab>"
let g:UltiSnipsJumpBackwardTrigger = "<s-tab>"

" }}}
" Plugin: Easy Align {{{

" start interactive EasyAlign in visual mode (e.g. vipga)
xmap ga <plug>(EasyAlign)
" start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <plug>(EasyAlign)

" verilog non-blocking auto align
inoremap <silent> <= <=<esc>mzvip:EasyAlign/<=/<cr>`z$a<space>

" customized setting mainly for verilog
" d: for aligning port/signal list with last space before ,;=)
let g:easy_align_delimiters = {
            \ '/': {
            \     'pattern':         '//\+\|/\*\|\*/',
            \     'delimiter_align': 'l',
            \     'ignore_groups':   ['!Comment'] 
            \     },
            \ ':': {
            \     'pattern':       ':',
            \     'left_margin':   1,
            \     'right_margin':  1,
            \     },
            \ '?': {
            \     'pattern':       '?',
            \     'left_margin':   1,
            \     'right_margin':  1,
            \     },
            \ '(': {
            \     'pattern':       '(',
            \     'left_margin':   1,
            \     'right_margin':  0,
            \   },
            \ ')': {
            \     'pattern':       ')',
            \     'left_margin':   0,
            \     'right_margin':  0,
            \   },
            \ 'd': {
            \     'pattern':      ' \ze\S\+\s*[,;=]',
            \     'left_margin':  0,
            \     'right_margin': 0
            \   }
            \ }

" }}}
" Plugin: Fugitive {{{

" select from target (left)
nnoremap gh[ :diffget //2<cr> :diffupdate<cr>
" select from merge (right)
nnoremap gh] :diffget //3<cr> :diffupdate<cr>

" }}}
" Plugin: CtrlP {{{

let g:ctrlp_map = '<c-p>'
let g:ctrlp_cmd = 'CtrlP'
let g:ctrlp_working_path_mode = 'ra'
let g:ctrlp_show_hidden = 0
let g:ctrlp_custom_ignore = {
            \ 'dir': '\v[\/]\.(git)$',
            \ 'file': '\v\.(DS_Store)$',
            \ }
let g:ctrlp_user_command = ['.git', 'git --git-dir=%s/.git ls-files -co --exclude-standard']

" Argument: focus, byfname, s:regexp, prv, item, nxt, marked
"           a:1    a:2      a:3       a:4  a:5   a:6  a:7
function! CtrlPMainStatus(...)
    let prv = '%#Comment# ' . a:4 . ' %*'
    let item = '%#PmenuSel# ' . (a:5 == 'mru files' ? 'mru' : a:5) . ' %*'
    let nxt = '%#Comment# ' . a:6 . ' %*'
    let dir = '%#Comment# « %*%4*' . fnamemodify(getcwd(), ':~') . '%* '

    " only output current mode and dir
    return '%#Comment# » %*' . item . '%#Comment# « ' . '%=%<' . dir
endfunction

" Argument: len
"           a:1
function! CtrlPProgressStatus(...)
    let len = '%#Function# ' . a:1 . ' %*'
    let dir = ' %=%<%#LineNr# ' . getcwd() . ' %*'
    return len . dir
endfunction

let g:ctrlp_status_func = {
            \ 'main': 'CtrlPMainStatus',
            \ 'prog': 'CtrlPProgressStatus'
            \ }

nnoremap gp :CtrlPBuffer<cr>

" }}}

" modeline
" vim:foldmethod=marker:foldmarker={{{,}}}:foldlevel=0:
